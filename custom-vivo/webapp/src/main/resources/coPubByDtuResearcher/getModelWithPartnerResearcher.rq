CONSTRUCT {
    ?pub a wos:Publication ;
        rdfs:label ?title ;
        tmp:pubDate ?date ;
        wos:wosId ?wosId ;
        wos:referenceCount ?refCount ;
        wos:citationCount ?citeCount ;
        bibo:doi ?doi ;
        tmp:relates ?authorship .
    ?authorship a tmp:Authorship ;
        tmp:name ?name ;
        tmp:relates ?collab .
} WHERE {  
    ?collab a wos:UnifiedOrganization ;
        vivo:relatedBy ?address .
    ?address a wos:Address ;
        vivo:relates ?pub .
    ?pub a wos:Publication ;
        rdfs:label ?title ;
        vivo:dateTimeValue ?dtv ;
        vivo:relatedBy ?address .
    ?dtv vivo:dateTime ?date .
    FILTER("2014-01-01T00:00:00"^^xsd:dateTime <= ?date)
    FILTER("2019-12-31T23:59:59"^^xsd:dateTime >= ?date)
    ?pub vivo:relatedBy ?dtuAddress .
    ?dtuAddress a wos:Address .
    ?dtuAddress vivo:relates d:org-technical-university-of-denmark .
    ?pub vivo:relatedBy ?authorship .
    ?authorship a vivo:Authorship .
    ?authorship vivo:relates ?dtuAddress .
    ?authorship vivo:relates ?dtuResearcher .
    ?address vivo:relatedBy ?partnerAuthorship .
    ?pub vivo:relatedBy ?partnerAuthorship .
    ?partnerAuthorship a vivo:Authorship .
    ?partnerAuthorship vivo:relates ?partnerResearcher .  
    ?partnerResearcher a foaf:Person .
    ?partnerResearcher rdfs:label ?partnerResearcherFullName .
    FILTER(?partnerResearcher != ?dtuResearcher)
    OPTIONAL {
        ?pub wos:wosId ?wosId ;
            wos:referenceCount ?refCount ;
            wos:citationCount ?citeCount .
    }
    OPTIONAL {
        ?pub bibo:doi ?doi
    }
}
